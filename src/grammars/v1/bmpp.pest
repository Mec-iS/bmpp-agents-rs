WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

Program = { SOI ~ Protocol+ ~ EOI }

Protocol = { ProtocolName ~ "<Protocol>" ~ Annotation ~ "{" ~ RolesSection ~ ParametersSection ~ InteractionSection ~ "}" }

ProtocolName = { Identifier }

RolesSection = { "roles" ~ RoleDecl ~ ("," ~ RoleDecl)* }

RoleDecl = { Identifier ~ "<Agent>" ~ Annotation }

ParametersSection = { "parameters" ~ ParameterDecl ~ ("," ~ ParameterDecl)* }

ParameterDecl = { Identifier ~ "<" ~ BasicType ~ ">" ~ Annotation }

InteractionSection = { InteractionItem+ }

InteractionItem = { StandardInteraction | ProtocolComposition }

StandardInteraction = { RoleRef ~ "->" ~ RoleRef ~ ":" ~ ActionName ~ "<Action>" ~ Annotation ~ "[" ~ ParameterFlowList? ~ "]" }

ProtocolComposition = { ProtocolReference ~ "[" ~ CompositionParameterList? ~ "]" }

ProtocolReference = { Identifier ~ "<Enactment>" }

CompositionParameterList = { CompositionParameter ~ ("," ~ CompositionParameter)* }

CompositionParameter = { (Direction ~ Identifier) | Identifier }

ParameterFlowList = { ParameterFlow ~ ("," ~ ParameterFlow)* }

ParameterFlow = { Direction ~ Identifier }

Direction = { "in" | "out" }

RoleRef = { Identifier }

ActionName = { Identifier }

BasicType = { "String" | "Int" | "Float" | "Bool" }

Annotation = { "(" ~ StringLiteral ~ ")" }

StringLiteral = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

Identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
